"../prelude.sk" import

[ :NUM :STR :TRUE :FALSE :NULL :VAR :AST ] enum

:new-var { VAR swap 2 implode } def
:new-num { NUM swap 2 implode } def
:new-str { STR swap 2 implode } def
:new-bool { TRUE FALSE ifl 1 implode } def
:new-null [ NULL ] defl

:=` { "=" var 2 wrap } def

:to-str {
	rev explode dup [
		NUM =` => { pop }
		STR =` => { pop }
		TRUE =` => { pop "true" }
		FALSE =` => { pop "false" }
		NULL =` => { pop "null" }
		default => { run' to-str }
	] switch
} def

:to-num {
	rev explode dup [
		NUM =` => { pop }
		STR =` => { pop 0 + }
		TRUE =` => { pop 1 }
		FALSE =` => { pop 0 }
		NULL =` => { pop 0 }
		default => { run' to-num }
	] switch
} def

:to-bool {
	rev explode dup [
		NUM =` => { pop }
		STR =` => { pop "" ne }
		TRUE =` => { pop true }
		FALSE =` => { pop false }
		NULL =` => { pop false }
		default => { run' to-bool }
	] switch
} def

:dump {
	rev explode [
		NUM =` => { "Number(" swap . ")" . }
		STR =` => { "String(" swap . ")" . }
		TRUE =` => { "Boolean(true)" }
		FALSE =` => { "Boolean(false)" }
		NULL =` => { "Null()" }
		VAR =` => { "Variable(" swap . ")" . }
		AST =` => { "todo:\sAST debug" die }
	 	default => { "invalid\stype:\s" swap . die }
	] switch print
} def

:run { rev explode run' } def
:run' {
	[
	 	NUM =` => { new-num }
	 	STR =` => { new-str }
	 	TRUE =` => { true new-bool }
	 	FALSE =` => { false new-bool }
	 	NULL =` => { new-null }
	 	VAR =` => { fetch-var }
	 	AST =` => { call }
	 	default => { "invalid\stype:\s" swap . die }
	] switch
} def
